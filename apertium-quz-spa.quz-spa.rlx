DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>" ;
SOFT-DELIMITERS = "<,>" ;

SETS

LIST N = n ; 
LIST A = adj ;
LIST Prop = (np ant) ; 
LIST Vmov = vm ;
LIST V = vblex ;
LIST Num = num ;    # posiblemente (num nom) o num.ord, num.px3sg/pl

LIST Gen = gen ;    # -q/pa
LIST Acc = acc ;    # -ta
LIST Per = per ;    # -nta
LIST P3acc = (px3sg acc) ;      # -n-ta
LIST Inf = inf ;        # -y
LIST N-Acc = (n acc) ;
LIST Nomin = inf ag pp pf ;     # nominalizadores -y, -q, -sqa, -na
LIST Caso = acc gen ben dat ter per abl loc ins csal dis prox posic ;   # sin nominativo

LIST Clit-NoImp = dfnt contr topi rnd af rep cnj dub ;
#                -puni -taq -qa  -ri -mi -si -chá -chus
# clíticos no usados con el imperativo - pero ???  cnti dcnt adit (raq ña pas), contr (taq) ?? dfnt (puni)?


LIST Clit = contr topi rnd qst af rep cnj dub enf ; # clíticos oracionales (sin los independientes)
#          -taq   -qa -ri -chu -mi -si -chá -chus -ya


LIST Anim = "irqi" "wawa" "wamra" "warma" "willka" 
        "tayta-mama" "mama-tayta" "qhari-warmi" "warmi-qhari" 
        "pana-tura" "wayqi-pana" "ñaña-tura"        
        (n m) (n f) (np ant) ;
## animados

SET  NumDet = Num + (nom) - Clit - Caso ;
SET  NominN = Nomin - (wa) ;    # también -su.. cuando no puede ser verbo subordinado


SECTION

SELECT A IF (-1 A) (1C N);
    # Jaimeqa usqhaylla phawashan hatun machu sach'aman, Maríamantan pakakushan.

REMOVE Per IF (NOT *0 Vmov) (0 P3acc) ;
		# Mamanta rikun.
		
REMOVE Per IF (0 Anim LINK 0 P3acc) ;
        # es más probable un movimiento hacia una persona que por una persona (aunque: la lanza pasó por...)


REMOVE ("riy" refl) IF  (0 ("rikuy")) ;
    ## para seleccionar riku-y "ver" en vez de ri-ku-y "ir" + "refl" (pero: ri....chi- ???)

SELECT Inf IF (0 N-Acc LINK *0 V) ;
# ( N | INF ) Acc + vblex tv --> INF

# IMPERATIVO | INF + NOM
REMOVE (imp) IF (0 Clit-NoImp) ;
REMOVE (imp) IF (1* ("<?>")) ;
REMOVE (imp) IF (0* ("mana") LINK 0 Clit) ; # + puni dfnt ? (excluir neg. de palabra, subord)
SELECT (imp) IF (-1* ("ama")) (0 (qst)) ; # BARRIER (imp) ???
SELECT (imp) IF (0 (enf)) ;
SELECT (imp) IF (1 ("<!>")) ;
# select (imp) si es la única palabra en la oración
# select imp if not 0* (mi/si/chá/chu..?) y hay por lo menos n palabras ???


  # pero: tukuy N --> det, prn (no adj o N-px1sg)
REMOVE ("tuku") IF (0 ("tukuy")) ;  # desbuhando
SELECT ("tukuy" det) IF (1 N) ; # + adj.. ver excepc. (+pos..), verbo inf..

SELECT A IF (0 N LINK 0 (nom) LINK 1C N) ;
  # wira runa - "hombre gordo", y no "hombre de grasa"
  
SELECT N IF (0 A LINK 0 Caso) ;
  # warmita "mujer", no "hembra"

# chaymi (antes de select prn/det.dem)
# SELECT ("chaymi") IF (-1 (<<<) LINK 1C* V BARRIER ("<,>")) ; - no funciona <<<
SELECT ("chaymi") IF (1C* V BARRIER ("<,>")) ; # primera palabra o desp. de <,>

# pronombres demostrativos - determinantes - adverbios
REMOVE (adv subst dem) IF (0 (det dem)) ;
SELECT (det dem) IF (0 (prn dem) LINK 1 N) ;
SELECT (det dem) IF (0 (prn dem) LINK 1 A LINK 1 N) ;
SELECT (det dem) IF (0 (prn dem) LINK 1 A LINK 1 A LINK 1 N) ;
SELECT (det dem) IF (0 (prn dem) LINK 1 A LINK 1 ("<,>") LINK 1 A LINK 1 N) ;
SELECT (det dem) IF (0 (prn dem) LINK 1 (preadj) LINK 1 A LINK 1 N) ;
SELECT (det dem) IF (0 (prn dem) LINK 1 NumDet LINK 1 N) ;
SELECT (det dem) IF (0 (prn dem) LINK 1 NumDet LINK 1 A LINK 1 N) ;
SELECT (det dem) IF (0 (prn dem) LINK 1 NumDet LINK 1 A LINK 1 A LINK 1 N) ;
# SELECT (adv subst dem) IF (0 (det dem) OR (prn dem)) ; # LINK 0 Clit) ; # con algunos casos sólo
# + numerales compuestos


# ima "y" antes de sel. pron/det.itg


# pronombres interrogativos -determinantes (indef/neg ?) (ADV.itg ?)
SELECT (det itg) IF (0 (prn itg) LINK 1 N) ;
SELECT (det itg) IF (0 (prn itg) LINK 1 A LINK 1 N) ;
SELECT (det itg) IF (0 (prn itg) LINK 1 A LINK 1 A LINK 1 N) ;
SELECT (det itg) IF (0 (prn itg) LINK 1 A LINK 1 ("<,>") LINK 1 A LINK 1 N) ;
SELECT (det itg) IF (0 (prn itg) LINK 1 (preadj) LINK 1 A LINK 1 N) ;
# con numerales???
# det.itg que no son prn.itg - no hace falta desambiguar


# pronombres indefinidos - determinantes
SELECT (det ind) IF (0 (prn) LINK 1 N) ;
SELECT (det ind) IF (0 (prn) LINK 1 A LINK 1 N) ;
SELECT (det ind) IF (0 (prn) LINK 1 A LINK 1 A LINK 1 N) ;
SELECT (det ind) IF (0 (prn) LINK 1 A LINK 1 ("<,>") LINK 1 A LINK 1 N) ;
SELECT (det ind) IF (0 (prn) LINK 1 (preadj) LINK 1 A LINK 1 N) ;
SELECT (det ind) IF (0 (prn) LINK 1 NumDet LINK 1 N) ;
SELECT (det ind) IF (0 (prn) LINK 1 NumDet LINK 1 A LINK 1 N) ;
SELECT (det ind) IF (0 (prn) LINK 1 NumDet LINK 1 A LINK 1 A LINK 1 N) ;


# pronombres cuantitativos - determinantes - preadjetivos - adverbios
# es difícil distinguir si es ADV o PRN+acc :-(
SELECT (preadj) IF (1C A) ;
SELECT (det qnt) IF (1C N) ;
# algunos son también ADJ - sinchi (+ nom), manchay, waliq
# otros verbos nominalizados - pasaq, llasaq, khuyay, tunpa(n)
# distinguir as(lla) "poco" de as+...+wan "más" (poner as "más" como aswan?)
# asta - prep, tukuy

REMOVE (evid) IF (1* V) ;
# revisar, talvez BARRIER ("<,>")

SELECT N IF (0 (ag) LINK -1 A OR (det)) ; # -1C ?

# para los nominalizadores (si existe N) select nom si va con det
# SELECT N IF (0 Inf OR (imp) LINK -1C (det) ; # - ojo, revisar para subord, tipos de det..

# select (af) if (0 (px3sg) LINK NOT *0C Clit) ;
# o remove (af) if (0 (px3sg) LINK *0C Clit) ;


END

# ---------------------------------------------------

## algo para prn.dem × adv × conj (prn.dem × adj/det.dem)
#select (det dem) if (0 (prn dem)) ( (1 N) or (1 ADJ) (2 N) or (1 ADJ) (2 ADJ) (1 N) )
# = select (prn dem nom)

# "<asta>": select "asta" (no "as" (acc))


# chaymi "por eso": selec si no sige verbo en 3sg/pl
# - al prinicpio, desp. de comilla o espacio
# "eso, ese" = sujeto, objeto, pred. nominal
# ??? X qhawashan = "ese"/"por" eso mira :-(

# chay, kay, haqay - prn.dem/adv.subst.dem/(det.dem)
# nom = prn.dem o cnjadv (chaymi) -> remove adv
# caso no loc (man, manta, kama, pi, nta, ta?) -> remove adv
# pero siempre puede ser prn/adv :-(
# man, manta, kama, nta + vm -> adv (pero sacar de allí.. = de eso?)
# pi - + kay, tiyay, puñuy.... (mucho :( )
# manta - + vm --> adv
# kama - + vm --> adv
# nta - + vm = adv
# man - + vm --> adv
# ta - + vm --> adv, pero tb. uso adverbial
# PERO vm sin caus !!! - desp. ambivalente !!! (-ta)
# prn - tiene referente anafórico... pero..???

# lemas por desambiguar:
# "aswan" "aswa" select aswan
# lluychu "venado" vs. lluy-chu "todo<qst>"
# kay - dem, vbcop/vbes + imp/inf
# chay-mi - chaymi
# huk/huq(nin)
# waki - "otros, algunos" prn/det vs. "parte" N
# tukuy - "todo" det/prn, "entero" ADJ, "mi buho" N, "terminar" vblex
# pasaq - det/prn, "pasar" vblex+ag (adv: + (lla) ña|pi)
# q'ala - "todo" prn/det, "calato" ADJ
# llasaq - prn/det/adv, "pesar" ag (llasaqta)
# allin - preadj, "bueno" adj
# sinchi - "fuerte" adj, "bastante" det/prn/preadj/adv
# manchay - "terrible" preadj/adj, "espantar/tener miedo" vblex
# aswan - "chicha" + -n, "más" prn + acc / adv
# waliq - "valer" vblex ag, "bueno" (lla) adj, "harto" det/prn
# tunpa(n) - "mostrar, indicar" vblex + -n..., "poco" prn/det/adv
# khuyay - "conmoverse" vblex inf/imp (qa, mi), "demasiado" prn/det/preadj/adv
# asta - "poco" prn + acc, "hasta" prep
# as - aswan - "un poco" sin wan, "más" con wan, a veces ambos :-(
# ima - "qué" prn, (+ ima sumaq preadj), "y" cnjcoo
# pi-cha - "barrer" vblex imp/inf, "quien" prn dim/conj - + (n..) POS --> verbo
# may-lla - "llavar", "dónde" = --^ (adv - sólo algunos casos)
# pi - + POS mejor pini-



# NULL-SECTION
# no se ejecuta

          ## AND = LINK 0, o + (+, -, OR = sets; link = entre tests)
          # (test) AND (test) AND (test) = (test LINK test LINK test)
          # (test) (test) = (test) AND (test)
          # ??? (test) OR (test) - ok (= varios tests: sel blah, sel blaj...)
          # (tag) (tag) = (tag) OR (tag)
          # (tag) + (tag) = (tag) AND (tag)
          # SELECT ("<wordform>"i) + (tag) ; ( "<wordform>"i SELECT (tag) ; - no funciona)